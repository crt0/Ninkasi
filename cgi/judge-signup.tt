#!/usr/bin/perl -T

use strict;
use warnings;

use lib '[% install_base %]/lib/perl5';

use File::Spec;
use Taint::Util;

BEGIN {
    if ( exists $ENV{NINKASI_TEST_SERVER_ROOT} ) {
        my $blib = File::Spec->catfile($ENV{NINKASI_TEST_SERVER_ROOT},
                                       File::Spec->updir(), 'blib');
        untaint $blib;
        require lib;
        lib->import( File::Spec->catfile($blib, 'lib' ),
                     File::Spec->catfile($blib, 'arch') );
    }
}

use Ninkasi::CGI;
use Ninkasi::Config;
use Ninkasi::Judge;
use Ninkasi::JudgeSignup;
use Ninkasi::Template;

MAIN: {
    my $cgi_object = Ninkasi::CGI->new();
    my $cgi_param = $cgi_object->Vars();

    my $template_name = 'judge_form.html';
    my %template_variable = (
        categories => \@Ninkasi::JudgeSignup::CATEGORIES,
        form       => $cgi_param,
        ranks      => \@Ninkasi::Judge::RANKS,
        title      => 'Brewers Cup Judge Volunteer Form',
    );
    if (%$cgi_param) {
        eval { Ninkasi::JudgeSignup::store $cgi_param };
        if ($@) {
            if (ref $@) {
                $template_variable{user_errors} = $@->{messages};
                $template_variable{error_field} = $@->{field   };
            }
            else {
                my $error_message = $@;
                $template_variable{system_error} = $error_message;
                eval {
                    Ninkasi::JudgeSignup::alert_maintainer $error_message,
                                                           $cgi_param;
                };
                if ($@) {
                    warn $@;
                }
            }
        }
        else {
            $template_name = 'confirmation.tt';
            $template_variable{title}
                = 'Brewers Cup Judge Volunteer Confirmation';
            $template_variable{type} = 'html';

            my $config = Ninkasi::Config->new();
            # if ( !$config->testing() ) {
            eval {
                Ninkasi::JudgeSignup::mail_confirmation $cgi_param;
            };
            if ($@) {
                warn $@;
            }
            # }
        }
    }

    my $template_object = Ninkasi::Template->new();
    $template_object->process($template_name, \%template_variable)
        or warn $template_object->error();
}
